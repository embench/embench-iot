# Copyright (C) 2014 Embecosm Limited.

# Contributor Andrew Burgess <andrew.burgess@embecosm.com>

# This file is part of Embench and was formerly part of the Bristol/Embecosm
# Embedded Benchmark Suite.

# SPDX-License-Identifier: GPL-3.0-or-later

if { [catch { exec which perf } perf_prog] } {
    verbose -log "Could not find `perf' program, skipping..."
    continue
}

verbose -log "Perf program = ${perf_prog}"

# The filehandle for the results file.
set perfstats_results ""

# Called once before any tests are run.
proc beebs_setup_before_all_benchmarks { } {
    global perfstats_results

    set filename "[beebs_results_root]/perfstats.csv"
    set perfstats_results [open $filename w]
    puts $perfstats_results "benchmark,instructions,branches,cycles"
    return 1
}

# Called once after all the tests have run.
proc beebs_cleanup_after_all_benchmarks { } {
    global perfstats_results

    close $perfstats_results
    file delete "[beebs_results_root]/perfstats.tmp"

    return 1
}

proc beebs_run_benchmark { benchmark filename } {
    global perf_prog
    global perfstats_results

    set results_file "[beebs_results_root]/perfstats.tmp"

    beebs_load "${perf_prog}" "stat -r5 -x, \"-o${results_file}\" \"${filename}\""

    set instructions [exec grep ,instructions, ${results_file} | cut -d, -f1]
    set branches [exec grep ,branches, ${results_file} | cut -d, -f1]
    set cycles [exec grep ,cycles, ${results_file} | cut -d, -f1]

    puts $perfstats_results "$benchmark,$instructions,$branches,$cycles"

    return 1;
}

beebs_run_all_benchmarks
